plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}




// Load values from SA properties file.
def propsFile = rootProject.file('sa.properties')
def props = new Properties()
props.load(new FileInputStream(propsFile))

repositories {
    mavenLocal()
    // Millicast SDK via Maven from GitHub Packages
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/millicast/maven")
        credentials {
            username = props['githubUsername']
            password = props['githubPat']
        }
    }
}

android {
    signingConfigs {
        release {
            storeFile file(props['keyStoreFile'])
            storePassword props['keyStorePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.millicast.android_java_sdk_sa"
        minSdkVersion 33
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 33
        versionCode 11
        versionName "1.7.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace 'com.millicast.android_app'
}

dependencies {

    implementation "androidx.annotation:annotation:1.3.0"
    implementation 'androidx.core:core:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.4.1'
    implementation 'androidx.navigation:navigation-ui:2.4.1'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.core:core-ktx:1.10.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Add Millicast SDK via one of the methods below.
    // For download via Maven from GitHub Packages:
    implementation 'com.millicast:millicast-sdk-android:1.7.0'
    implementation 'io.dolby:promise:2.9.0'
    // For manual addition of AAR file:
    // implementation project(":MillicastSDK")
}

// Log the version of R8 used.
println("Using R8: ${com.android.tools.r8.Version.getVersionString()}")
